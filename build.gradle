/**
 * Copyright (c) 2018-present, A2 Rešitve d.o.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in
 * compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is
 * distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See
 * the License for the specific language governing permissions and limitations under the License.
 */

plugins {
	id "java-library"
	id "com.github.johnrengelman.shadow" version "7.1.2"
	id "org.owasp.dependencycheck" version "8.2.1"
	id "maven-publish"
    id "signing"
}

java {
	withSourcesJar()
	withJavadocJar()
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

repositories {
	mavenCentral()
}

import java.nio.file.Paths

def readVersion() {
	def versionPropsFile = file(Paths.get("src","main","resources","orajdbc-proxy-version.properties"))
	if (versionPropsFile.canRead()) {
		Properties versionProps = new Properties()

		if (versionPropsFile.canRead()) {
			versionProps.load(new FileInputStream(versionPropsFile))
		}
		versionProps['version']
	} else {
		throw new GradleException("Could not read version.properties!")
	}
}

version = readVersion()
group "solutions.a2.oracle"

repositories {
	mavenCentral()
}

dependencies {
    def oracleJdbcVersion = "21.10.0.0"
    // https://mvnrepository.com/artifact/com.oracle.database.jdbc/ojdbc11
    compileOnly("com.oracle.database.jdbc:ojdbc11:$oracleJdbcVersion")
    testImplementation("com.oracle.database.jdbc:ojdbc11:$oracleJdbcVersion")

    def chronicleMapVersion = "3.23.5"
    // https://mvnrepository.com/artifact/net.openhft/chronicle-map
    implementation("net.openhft:chronicle-map:$chronicleMapVersion")
    def chronicleAffinityVersion = "3.23.3"
    // https://mvnrepository.com/artifact/net.openhft/affinity
    implementation("net.openhft:affinity:$chronicleAffinityVersion")

    def jUnitVersion = "5.9.2"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation("org.junit.jupiter:junit-jupiter-api:$jUnitVersion")
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testImplementation("org.junit.jupiter:junit-jupiter-engine:$jUnitVersion")
}


jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes "Main-Class": "solutions.a2.oracle.jdbc.OraProxyFileUtils"
    }
//    from {
//        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation
shadowJar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes "Main-Class": "solutions.a2.oracle.jdbc.OraProxyFileUtils"
    }
}

test {
	useJUnitPlatform {
		excludeTags 'ignored'
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Oracle JDBC Proxy Driver"
                packaging = "jar"
                url = "https://github.com/averemee-si/orajdbc-proxy"
                description =
                    "Oracle JDBC Proxy Driver is designed to change the SQL statements being executed against Oracle Database " + 
                    "without changing the JVM based application (Spring/J2EE/servlets etc) itself."
                organization {
                    name = "A2 Rešitve, d.o.o."
                    url = "https://a2-solutions.eu/"
                }
                scm {
                    url = "https://github.com/averemee-si/orajdbc-proxy"
                    connection = "scm:git:https://github.com/averemee-si/orajdbc-proxy.git"
                    developerConnection = "scm:git:git@github.com:averemee-si/orajdbc-proxy.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://a2-solutions.eu/licenses/apache/LICENSE.txt"
                    }
                }
                developers {
                    developer {
                        id = "averemee-si"
                        name = "Aleksei Veremeev"
                        email = "averemee@a2.solutions"
                        timezone = "Europe/Ljubljana"
                        organization = "A2 Rešitve, d.o.o."
                        organizationUrl = "https://a2-solutions.eu/"
                    }
                }
            }
        }
	}

	repositories {
	    maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
	        credentials {
	            username = project.hasProperty('ossrh.Username') ? project.property('ossrh.Username') : ""
	            password = project.hasProperty('ossrh.Password') ? project.property('ossrh.Password') : ""
	        }
	    }
	}
}

signing {
    def signingKey = project.hasProperty('signing.Key') ? project.property('signing.Key') : ""
    def signingPassword = project.hasProperty('signing.Password') ? project.property('signing.Password') : ""
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
